%----------------------------------------------------------------------------
\clearpage\section{Spacecraft Model}\label{sec:spacecraft-model}
%----------------------------------------------------------------------------

\begin{lstlisting}[language=statechart, caption={The wrapper synchronous component.}]
package mission

import "Interface/Interfaces"
import "Groundstation/GroundStation"
import "Spacecraft/Spacecraft"

sync Mission [
	port _control : requires StationControl
] {
	component station : GroundStation
	component satellite : Spacecraft
	bind _control -> station._control
	channel [ satellite.connection ] -o)- [ station.connection ]
}
\end{lstlisting}

\begin{lstlisting}[language=statechart, caption={The reachability properties generated by Gamma.}]
import "/hu.bme.mit.jpl.spacemission.casestudy/model/.Mission.gsm"
component Mission
@ ("station.Main.Idle")
E F [ { state station.Main.Idle } ]
@ ("station.Main.Operation")
E F [ { state station.Main.Operation } ]
@ ("satellite.Communication.Transmitting")
E F [ { state satellite.Communication.Transmitting } ]
@ ("satellite.Battery.Recharging")
E F [ { state satellite.Battery.Recharging } ]
@ ("satellite.Communication.WaitingPing")
E F [ { state satellite.Communication.WaitingPing } ]
@ ("satellite.Check.CheckTimeout")
E F [ { state satellite.Check.CheckTimeout } ]
@ ("satellite.Battery.NotRecharging")
E F [ { state satellite.Battery.NotRecharging } ]
\end{lstlisting}

\begin{lstlisting}[language=statechart, caption={The definition of the run analysis and verification tasks.}]
import "Interface/Interfaces.gcd"
import "Mission.gcd"

analysis {
	component : Mission
	language : XSTS-UPPAAL
	state-coverage
	constraint : {
		minimum-orchestrating-period : SCHEDULE_CONSTRAINT ms
		maximum-orchestrating-period : SCHEDULE_CONSTRAINT ms
	}
}

verification {
	language : XSTS-UPPAAL
	file : "Mission.xml"
	property-file : ".Mission.gpd"
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The interface model specifying the signals between the two components.},linewidth=0.85\textwidth, xleftmargin=0.15\textwidth]
timeout waitTimeout
activity Calculate (out a : integer) [language=action] {
	self.a := 10;
}
activity Example {
	var even : boolean := false
	
	initial Initial
	merge Merge
	decision Decision
	action SetWait : activity [language=action] {
		set waitTimeout := 10s;
	}
	trigger Wait when timeout waitTimeout
	action Calc : Calculate
	action Process : activity (in b : integer) [language=action] {
		even := self.b mod 2 = 0;
	}
	final Final
	
	control flow from Initial to Merge
	// ...
	data flow from Calc.a to Process.b
	control flow from Decision to SetWait [even]
	control flow from Decision to Final [!even]
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The Gamma implementation of the ReceiveData activity.}]
activity ReceiveData {
	initial Initial
	
	merge Merge
	
	trigger DataPacket when connection._data
	
	action ProcessData
	
	control flow from Initial to Merge
	control flow from Merge to DataPacket
	control flow from DataPacket to ProcessData
	control flow from ProcessData to Merge
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The Gamma implementation of the Ground Station component.}, linewidth=0.80\textwidth, xleftmargin=0.20\textwidth]
statechart GroundStation [
	port connection : requires DataSource
	port _control : requires StationControl
] {
	region Main {
		initial Entry
		state Idle
		state Operation {
			do / call ReceiveData;
			entry / raise connection.ping;
		}
	}
	
	transition from Idle to Operation when _control.start
	transition from Operation to Idle when _control.shutdown
	
	activity ReceiveData {
		// ...
	}
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The Gamma implementation of the Recharge Batteries activity.}]
activity RechargeBatteries {
	initial Initial
	
	merge Merge
	decision Decision
	action SetWait : activity [language=action] {
		set rechargeTimeout := 10s;
	}
	trigger Wait when timeout rechargeTimeout
	action Charge : activity [language=action] {
		batteryVariable := batteryVariable + 1;
	}
	action Full : activity [language=action] {
		batteryFullyCharged := true;
	}
	final Final
	
	control flow from Initial to Merge
	control flow from Merge to Decision
	control flow from Decision to SetWait [batteryVariable < 100]
	control flow from SetWait to Wait
	control flow from Wait to Charge
	control flow from Charge to Merge
	control flow from Decision to Full [batteryVariable >= 100]
	control flow from Full to Final
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The Gamma implementation of the Transmit Data activity.}, label={}]
activity TransmitData {				
	initial Initial
	
	fork Fork
	
	merge TransmitMerge
	decision TransmitDecision
	action SetTransmitWait : activity [language=action] {
		set transmitTimeout := 1s;
	}
	trigger TransmitWait when timeout transmitTimeout
	action TransmitData : activity [language=action] {
		_data := _data - 1;
		raise connection._data;
	}
	
	merge ConsumeMerge
	action SetConsumeWait : activity [language=action] {
		set consumeTimeout := 1s;
	}
	trigger ConsumeWait when timeout consumeTimeout
	action ConsumeEnergy : activity [language=action] {
		batteryVariable := batteryVariable - 1;
	}
	decision ConsumeDecision
	
	merge Merge
	
	action SetDone : activity [language=action] {
		transmitionDone := true;
	}
	
	final Final
	
	control flow from Initial to Fork
	
	control flow from Fork to TransmitMerge
	control flow from TransmitMerge to TransmitDecision
	control flow from TransmitDecision to SetTransmitWait [_data > 0]
	control flow from SetTransmitWait to TransmitWait
	control flow from TransmitWait to TransmitData
	control flow from TransmitData to TransmitMerge
	control flow from TransmitDecision to Merge [_data <= 0]
	
	control flow from Fork to ConsumeMerge
	control flow from ConsumeMerge to SetConsumeWait
	control flow from SetConsumeWait to ConsumeWait
	control flow from ConsumeWait to ConsumeEnergy
	control flow from ConsumeEnergy to ConsumeDecision
	control flow from ConsumeDecision to ConsumeMerge [batteryVariable >= 40]
	control flow from ConsumeDecision to Merge [batteryVariable < 40]
	
	control flow from Merge to SetDone
	control flow from SetDone to Final
}
\end{lstlisting}

\begin{lstlisting}[float,language=statechart, caption={The Gamma implementation of the Spacecraft component.}, label={}]
package spacecraft
import "Interface/Interfaces.gcd"
@RegionSchedule = bottom-up
statechart Spacecraft [
port connection : provides DataSource
] {
	var batteryVariable : integer := 100
	var _data : integer := 100
	
	var batteryRecharging : boolean := false
	var batteryFullyCharged : boolean := false
	var transmitionDone : boolean := false
	
	timeout rechargeTimeout
	timeout consumeTimeout
	timeout transmitTimeout
	
	timeout checkTimeout
	
	region Communication { 
		initial CommunicationEntry
		state WaitingPing
		state Transmitting {
			do / call TransmitData;
		}
	}
	region Battery {
		initial BatteryEntry
		state NotRecharging {
			entry / batteryRecharging := false;
		}
		state Recharging {
			do / call RechargeBatteries;
			entry / batteryRecharging := true;
		}
	}
	
	region Check {
		initial CheckEntry
		state CheckTimeout {
			entry / set checkTimeout := 500ms;
		}
	}
	
	transition from CheckEntry to CheckTimeout
	transition from CheckTimeout to CheckTimeout when timeout checkTimeout
	
	transition from CommunicationEntry to WaitingPing
	transition from WaitingPing to Transmitting 
		when connection.ping [!batteryRecharging and _data > 0]
	transition from Transmitting to WaitingPing when timeout checkTimeout [transmitionDone] 
		/ transmitionDone := false;
	
	transition from BatteryEntry to NotRecharging	
	transition from NotRecharging to Recharging when timeout checkTimeout [batteryVariable < 80]
	transition from Recharging to NotRecharging when timeout checkTimeout [batteryFullyCharged]
		/ batteryFullyCharged := false;
	
	activity TransmitData {		
		// ...
	}
	
	activity RechargeBatteries {
		// ...
	}
	
}
\end{lstlisting}
